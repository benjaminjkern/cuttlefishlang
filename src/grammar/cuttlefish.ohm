Cuttlefish {
   Specification = newline* Statement (newline Statement)* newline*

   Statement = 
    | Pattern "=" Expression -- assignment
    | print Expression -- print
    | put? Expression -- put
    
   Block<A, delimiter> =
    | newline indent newline* A (newline A)* newline* dedent &newline? -- large
    | NonemptyListOf<A, delimiter> delimiter? &newline? -- small

   Pattern = NonemptyListOf<PatternElement,",">
   PatternElement = 
    | "(" Pattern ")" -- tuple
    | ListType? Atom ":" id -- list
    | Type PatternElement -- typed
    | Atom
   Type = Type "=>" Type -- function
    | "(" NonemptyListOf<Type, ","> ")" -- tuple
    | ListType
    | id
   ListType = "[" Type "]"
   
   SubRoutineGroup = 
    | Block<SubRoutine, ";"> 
    | Block<Statement, ","> -- singular

   Routine = Function | Process | Server
   Function = fn ":" SubRoutineGroup
   Process = prc ":" SubRoutineGroup
   Server = srv ":" Block<SubRoutine, ";">
   
   SubRoutine = Pattern ("|" Expression)? "->" Block<Statement, ",">

   Expression = Atom+

   Atom =
    | AbstractType
    | Routine
    | Select
    | List
    | String
    | Tuple
    | numlit
    | "(" Expression ")" -- singleton
    | nospaceops 
    | id 

   AbstractType = Type "type" (":" Block<DataPattern, ";">)?
   DataPattern = (Pattern "->")? Block<Pattern, ",">
    
   Select = (List | Tuple | String | DynamicSequence | id) Selector
   Selector = "[" (Function | numlit | id) "]" -- nonSlice
    | List

   Tuple = 
    | "(" (Expression ",")+  Expression? ")" -- nonempty
    | "(" ")" -- empty
   List =
    | "[" "]" -- empty
    | "[" index ".." (index "..")? index? "]" -- num
    | Sequence<"[","]">
   index = numlit | id
   String = rawSequence<"\"","\"">
    | rawSequence<"\'","\'">
    | rawSequence<"`","`">
    
   numlit = digit+ ("." digit+)? (("E" | "e") ("+" | "-")? digit+)?
   
   print = "print" ~alnum
   put = "put" ~alnum
   fn = "fn" ~alnum
   prc = "prc" ~alnum
   srv = "srv" ~alnum
   
   indent = "⇨"
   dedent = "⇦"
	
   nospaceops = dynamicnospaceops | "~" | "+" | "++" | ">=" | "<=" | "!=" | "==" | "===" | "*"
   keyword = fn | prc | srv | print | put
   reserved = keyword | nospaceops | "(" | ")" | "," | "[" | "]" | "->" | "=>" | ":" | ":=" | "=" | ".." | "#" | ";" | "\n" | "|" | indent | dedent
   id = (~reserved ~space any)+

   dynamicnospaceops = "FOR COMPILE TIME USE"
   DynamicRawSequence = "FOR COMPILE TIME USE"
   DynamicSequence = "FOR COMPILE TIME USE"

   newline =  ("\n" comment?)+
   space :=  " " | "\t" | comment
   comment = 
    | "#" ~"/" (~"\n" any)* -- normal
    | rawSequence<"#/","/#">

   Sequence<open,close> = open ListOf<Atom,","> close
   rawSequence<open,close> = open (~open ~close any)*  close
}

